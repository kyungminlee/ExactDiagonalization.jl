using Test
using ExactDiagonalization

using Suppressor
using LinearAlgebra
using SparseArrays

using ExactDiagonalization.Toolkit: pauli_matrix

@testset "RedRep4" begin
  tol = Base.rtoldefault(Float64)

  n = 4
  unitcell = make_unitcell(1.0; OrbitalType=String)
  addorbital!(unitcell, "Spin", FractCoord([0], [0.0]))
  lattice = make_lattice(unitcell, n)

  # Test State and Site
  up = State("Up", 1)
  dn = State("Dn",-1)
  spin_site = Site([up, dn])

  hs = HilbertSpace(repeat([spin_site], n))
  σ = Dict( (isite, j) => pauli_matrix(hs, isite, j) for isite in 1:n, j in [:x, :y, :z, :+, :-])
  j1 = sum(σ[i, j] * σ[mod(i, n) + 1 , j] for i in 1:n, j in [:x, :y, :z])
  j2 = sum(σ[i, j] * σ[mod(i+1, n) + 1 , j] for i in 1:n, j in [:x, :y, :z])

  hsr = represent(HilbertSpaceSector(hs, 0))
  tsym = TranslationSymmetry(lattice)
  tsymbed = embed(lattice, tsym)
  # translation_group = TranslationGroup([Permutation([2,3,4,1])])
  # @test isinvariant(hs, translation_group, j1)
  # @test isinvariant(HilbertSpaceSector(hs, 0), translation_group, j1)

  @testset "RHSR" begin
    #rhsr = symmetry_reduce(hsr, translation_group, [0//1])
    rhsr = symmetry_reduce(hsr, IrrepComponent(tsymbed, 1, 1))
    @test scalartype(rhsr) === ComplexF64
    @test scalartype(typeof(rhsr)) === ComplexF64
    @test valtype(rhsr) === ComplexF64
    @test valtype(typeof(rhsr)) === ComplexF64
    @test bintype(rhsr) === UInt
    @test bintype(typeof(rhsr)) === UInt
    @test dimension(rhsr) <= 2^n
    @test bitwidth(rhsr) == n
    show(devnull, MIME("text/plain"), rhsr)  # make sure it doesn't crash
  end

  j1_rep = represent(hsr, j1)
  j1_mat = Matrix(j1_rep)
  @test hsr.basis_list == [0b0011, 0b0101, 0b0110, 0b1001, 0b1010, 0b1100]

  @testset "ROR" begin
    #rhsr = symmetry_reduce(hsr, translation_group, [0//1])
    rhsr = symmetry_reduce(hsr, IrrepComponent(tsymbed, 1, 1))
    @test dimension(rhsr) == 2
    @test rhsr.basis_list == UInt[0b0011, 0b0101]
    dim = dimension(rhsr)

    j1_redrep = represent(rhsr, j1)
    j2_redrep = represent(rhsr, j2)
    show(devnull, MIME("text/plain"), j1_redrep) # make sure it doesn't crash

    @testset "typetraits" begin
      @test scalartype(j1) === Complex{Int}
      @test valtype(j1) === Complex{Int}

      @test scalartype(j1_redrep) === ComplexF64
      @test scalartype(typeof(j1_redrep)) === ComplexF64
      @test valtype(j1_redrep) === ComplexF64
      @test valtype(typeof(j1_redrep)) === ComplexF64

      @test bintype(j1_redrep) === UInt
      @test bintype(typeof(j1_redrep)) === UInt

      @test spacetype(j1_redrep) === typeof(rhsr)
      @test operatortype(j1_redrep) === typeof(j1)
      @test spacetype(typeof(j1_redrep)) === typeof(rhsr)
      @test operatortype(typeof(j1_redrep)) === typeof(j1)
      @test get_space(j1_redrep) === rhsr
    end

    @testset "property" begin
      @test bitwidth(j1_redrep) == bitwidth(rhsr)
      @test bitwidth(j2_redrep) == bitwidth(rhsr)
      @test dimension(j1_redrep) == dimension(rhsr)
      @test dimension(j2_redrep) == dimension(rhsr)
    end

    @testset "sym" begin
      @test ishermitian(represent(hsr, σ[2, :x]))
      @test !ishermitian(represent(hsr, σ[2, :+]))
    end

    @testset "unary operator" begin
      @test +j1_redrep == j1_redrep
      @test -j1_redrep == represent(rhsr, -j1)
    end

    @testset "binary operator" begin
      @test simplify(j1_redrep + j2_redrep) == represent(rhsr, simplify(j1 + j2))
      @test simplify(j1_redrep - j2_redrep) == represent(rhsr, simplify(j1 - j2))
      @test simplify(j1_redrep * j2_redrep) == represent(rhsr, simplify(j1 * j2))

      @test simplify(j1_redrep * 0) == represent(rhsr, NullOperator())
      @test simplify(0 * j1_redrep) == represent(rhsr, NullOperator())
      @test simplify(j1_redrep * 2) == represent(rhsr, simplify(j1 * 2))
      @test simplify(2 * j1_redrep) == represent(rhsr, simplify(2 * j1))
      @test simplify(j1_redrep / 2) == represent(rhsr, simplify(j1 / 2))
      @test simplify(2 \ j1_redrep) == represent(rhsr, simplify(2 \ j1))
    end

    let
      psis = [normalize([1.0, 0.0, 1.0, 1.0, 0.0, 1.0]), normalize([0.0, 1.0, 0.0, 0.0, 1.0, 0.0])]
      H = zeros(ComplexF64, (2,2))
      for i in 1:2, j in 1:2
        H[i,j] = psis[i] ⋅ (j1_mat * psis[j])
      end
      @test isapprox(Matrix(j1_redrep), H; atol=tol)

      @testset "get_row_iterator" begin
        rowvec = Vector{ComplexF64}(undef, dimension(rhsr))
        for irow_r in 1:dimension(rhsr)
          fill!(rowvec, zero(ComplexF64))
          err = zero(ComplexF64)
          for (icol_r, ampl) in get_row_iterator(j1_redrep, irow_r)
            if 1 <= icol_r <= dimension(rhsr)
              rowvec[icol_r] += ampl
            else
              err += ampl
            end
          end
          @test isapprox(err, 0; atol=tol) # is this necessarily true?
          @test isapprox(rowvec, H[irow_r, :]; atol=tol)
        end
      end

      @testset "get_column_iterator" begin
        colvec = zeros(ComplexF64, dimension(rhsr))
        for icol_r in 1:dimension(rhsr)
          colvec[:] .= zero(ComplexF64)

          colvec[:] .= zero(ComplexF64)
          err = zero(ComplexF64)
          for (irow_r, ampl) in get_column_iterator(j1_redrep, icol_r)
            if 1 <= irow_r <= dimension(rhsr)
              colvec[irow_r] += ampl
            else
              err += ampl
            end
          end
          @test isapprox(err, 0; atol=tol) # is this necessarily true?
          @test isapprox(colvec, H[:, icol_r]; atol=tol)
        end
      end # testset get_column_iterator

      @testset "get_element" begin
        for irow_r in 1:dimension(rhsr)
          for icol_r in 1:dimension(rhsr)
            @test isapprox(get_element(j1_redrep, irow_r, icol_r), H[irow_r, icol_r]; atol=tol)
            @test isapprox(j1_redrep[irow_r, icol_r], H[irow_r, icol_r]; atol=tol)
          end
        end
      end

      @testset "get exceptions" begin
        dim = dimension(rhsr)
        opr = j1_redrep
        @test_throws BoundsError get_row_iterator(opr, 0)
        @test_throws BoundsError get_row_iterator(opr, dim+1)
        @test_throws BoundsError get_column_iterator(opr, 0)
        @test_throws BoundsError get_column_iterator(opr, dim+1)
        @test_throws BoundsError get_row(opr, 0)
        @test_throws BoundsError get_row(opr, dim+1)
        @test_throws BoundsError get_column(opr, 0)
        @test_throws BoundsError get_column(opr, dim+1)
        @test_throws BoundsError get_element(opr, 0, 1)
        @test_throws BoundsError get_element(opr, dim+1, 1)
        @test_throws BoundsError get_element(opr, 1, 0)
        @test_throws BoundsError get_element(opr, 1, dim+1)
        @test_throws BoundsError opr[0, 1]
        @test_throws BoundsError opr[dim+1, 1]
        @test_throws BoundsError opr[1, 0]
        @test_throws BoundsError opr[1, dim+1]
      end
    end
  end # testset ROR


  @testset "ROR-ALL" begin
    j1_mat = Matrix(represent(hsr, j1))
    @test isapprox(j1_mat, adjoint(j1_mat); atol=tol)
    eigenvalues1 = eigvals(Hermitian(j1_mat))
    eigenvalues2 = Float64[]
    for tsym_irrep_index in 1:num_irreps(tsym)
      rhsr = symmetry_reduce(hsr, IrrepComponent(tsymbed, tsym_irrep_index))

      j1_redrep = represent(rhsr, j1)
      j1_redmat = Matrix(j1_redrep)
      @test isapprox(j1_redmat, adjoint(j1_redmat); atol=tol)
      append!(eigenvalues2, eigvals(Hermitian(j1_redmat)))
    end
    sort!(eigenvalues2)
    @test length(eigenvalues1) == length(eigenvalues2)
    @test isapprox(eigenvalues1, eigenvalues2; atol=tol)
  end # testset iterator

end # testset RedOp4

@testset "RedOp7" begin
  # testing complex phase
  n = 7
  unitcell = make_unitcell(1.0; OrbitalType=String)
  addorbital!(unitcell, "Spin", FractCoord([0], [0.0]))
  lattice = make_lattice(unitcell, n)

  tol = Base.rtoldefault(Float64)
  spin_site = Site([State("Up", 1), State("Dn",-1)])

  hs = HilbertSpace(repeat([spin_site], n))
  σ = Dict( (isite, j) => pauli_matrix(hs, isite, j) for isite in 1:n, j in [:x, :y, :z, :+, :-])
  j1 = sum(σ[i, j] * σ[mod(i, n) + 1 , j] for i in 1:n, j in [:x, :y, :z])

  hsr = represent(HilbertSpaceSector(hs, 0))
  # translation_group = TranslationGroup([Permutation([2,3,4,5,6,7,1])])
  # @test isinvariant(hs, translation_group, j1)
  # @test isinvariant(HilbertSpaceSector(hs, 0), translation_group, j1)

  j1_rep = represent(hsr, j1)
  mat_size = size(j1_rep)

  mat0 = Matrix(j1_rep)
  mat1 = zeros(ComplexF64, mat_size)
  mat2 = zeros(ComplexF64, mat_size)
  for i in 1:mat_size[1]
    for (j, v) in get_row_iterator(j1_rep, i)
      if j > 0
        mat1[i,j] += v
      end
    end
  end

  for j in 1:mat_size[2]
    for (i, v) in get_column_iterator(j1_rep, j)
      if i > 0
        mat2[i,j] += v
      end
    end
  end

  @test isapprox(mat0, mat1; atol=1E-8)
  @test isapprox(mat0, mat2; atol=1E-8)


end

@testset "RedOp-nontriv" begin
# TODO: at an angle
end
